Sub Auto_Table_Contents()

'Naming variables

    Dim StartCell As Range 'for input box to select range to apply macro
    Dim EndCell As Range   'for message box as info
    Dim Sh As Worksheet
    Dim ShName As String
    Dim MsgConfirm As VBA.VbMsgBoxResult 'this is for messagebox to confirm
    
    MsgConfirm = VBA.MsgBox("The values in cells could be overwritten. Would you like to continue?", vbOKCancel + vbDefaultButton2)
    If MsgConfirm = vbCancel Then Exit Sub
    
    
'If error, it will resume thru next 2 lines
    
    On Error Resume Next
    
    Set StartCell = Excel.Application.InputBox("Where do you want to insert the Table of Contents?" _
    & vbNewLine & "Please select a cell:", "Insert Table of Contents", , , , , , 8)
   
'If error 424, it will exit sub and if NOT 424, it will go to Handle at bottom
    
    If Err.Number = 424 Then Exit Sub
    On Error GoTo Handle
    

    
    Set StartCell = StartCell.Cells(1, 1)
    Set EndCell = StartCell.Offset(Worksheets.Count - 2, 1)
    
    'getting confirmation
    
    MsgConfirm = VBA.MsgBox("The values in cells:" & vbNewLine & StartCell.Address & "to" & EndCell.Address & _
    " could be overwritten." & vbNewLine & "Would you like to continue?", vbOKCancel + vbDefaultButton2, "Confirmation Required")
    If MsgConfirm = vbCancel Then Exit Sub
    
    
    For Each Sh In Worksheets
        ShName = Sh.Name
        If ActiveSheet.Name <> ShName Then
        
            If Sh.Visible = xlSheetVisible Then
            
                ActiveSheet.Hyperlinks.Add Anchor:=StartCell, Address:="", SubAddress:= _
                "'" & ShName & "'!A1", TextToDisplay:=ShName
                StartCell.Offset(0, 1).Value = Sh.Range("A1").Value
                Set StartCell = StartCell.Offset(1, 0)
            End If 'if sheet is visible
            
        End If 'if sheet is not the active sheet
        
    Next Sh
    Exit Sub
    
Handle:

    MsgBox "Unfortunately, an error has occured!"

End Sub
